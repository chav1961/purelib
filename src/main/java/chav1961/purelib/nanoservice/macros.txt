; --------------------------------------------------------------------

makeIncludes 			.macro		class2load:str
						.package 	chav1961.purelib.nanoservice
						.import		chav1961.purelib.basic.exceptions.ContentException
						.import		chav1961.purelib.basic.Utils
						.import		chav1961.purelib.basic.exceptions.FlowException
						.import		chav1961.purelib.basic.exceptions.EnvironmentException
						.import		chav1961.purelib.enumerations.MarkupOutputFormat
						.import		chav1961.purelib.streams.charsource.ReaderCharSource
						.import		chav1961.purelib.streams.chartarget.WriterCharTarget
						.import		chav1961.purelib.streams.char2char.CreoleWriter
						.import		chav1961.purelib.streams.JsonStaxParser
						.import		chav1961.purelib.streams.JsonStaxPrinter
						.import		chav1961.purelib.nanoservice.NanoServiceFactory$PathParser
						.import		chav1961.purelib.nanoservice.NanoServiceFactory$QueryParser
						.import		chav1961.purelib.nanoservice.NanoServiceFactory$RequestHeadParser
						.import		chav1961.purelib.nanoservice.NanoServiceFactory$ResponseHeadSetter
						.import		chav1961.purelib.nanoservice.NanoServiceFactory$AnyKindParserAndSetter
						.import		chav1961.purelib.basic.exceptions.SyntaxException
						.import		chav1961.purelib.nanoservice.interfaces.MethodExecutor
						.import		chav1961.purelib.nanoservice.interfaces.QueryType
						.import		chav1961.purelib.nanoservice.InternalUtils
						.import		com.sun.net.httpserver.Headers
						.import		java.io.Closeable
						.import		java.io.Flushable
						.import		java.io.InputStream
						.import		java.io.IOException
						.import		java.io.OutputStream
						.import		java.io.Reader
						.import		java.io.Writer
						.import		java.lang.StringBuilder
						.import		java.util.List
						.import		java.util.ArrayList
						.import		java.util.UUID
						.import		javax.xml.stream.XMLStreamWriter
						.import		org.w3c.dom.Document
						.import		&class2load
						.mend

; --------------------------------------------------------------------

getExecutorHead			.macro		name:str,simpleName:str,pathParsed:int=0,queryParsed:int=0,requestHeadParsed:int=0,totalResponsed:int=0
	getExecutorClassHead	"&name","&simpleName",pathParsed=&pathParsed,queryParsed=&queryParsed,requestHeadParsed=&requestHeadParsed,totalResponsed=&totalResponsed
	getExecutorConstructor	"&name","&simpleName",pathParsed=&pathParsed,queryParsed=&queryParsed,requestHeadParsed=&requestHeadParsed,totalResponsed=&totalResponsed
	getExecutorMethodHead	"&name","&simpleName",pathParsed=&pathParsed,queryParsed=&queryParsed,requestHeadParsed=&requestHeadParsed,totalResponsed=&totalResponsed
						.mend

; --------------------------------------------------------------------

getExecutorClassHead	.macro		name:str,simpleName:str,pathParsed:int=0,queryParsed:int=0,requestHeadParsed:int=0,totalResponsed:int=0
						.line		auto
						.source		"&simpleName"
&simpleName				.class		public extends java.lang.Object implements chav1961.purelib.nanoservice.interfaces.MethodExecutor
forProcessor			.field		java.lang.Object
		.if (pathParsed+queryParsed+requestHeadParsed > 0) 
forParsedParameters		.field		char[][]
			.if (pathParsed > 0)
forPath					.field		chav1961.purelib.nanoservice.NanoServiceFactory$AnyKindParserAndSetter private final 
			.endif
			.if (queryParsed > 0)
forQuery				.field		chav1961.purelib.nanoservice.NanoServiceFactory$AnyKindParserAndSetter private final 
			.endif
			.if (requestHeadParsed > 0)
forRequestHeader		.field		chav1961.purelib.nanoservice.NanoServiceFactory$AnyKindParserAndSetter private final 
			.endif
		.endif
		.if (totalResponsed > 0) 
forResponsedParameters	.field		java.lang.Object[]
forResponseHeader		.field		chav1961.purelib.nanoservice.NanoServiceFactory$AnyKindParserAndSetter private final
		.endif
						.mend

; --------------------------------------------------------------------

getExecutorClassTail	.macro		name:str
&name					.end
						.mend

; --------------------------------------------------------------------

getExecutorConstructor	.macro		name:str,simpleName:str,pathParsed:int=0,queryParsed:int=0,requestHeadParsed:int=0,totalResponsed:int=0
totalParsed				.local		int
totalParsed				.set		pathParsed+queryParsed+requestHeadParsed		
&simpleName				.method		void public
forPathP				.parameter	chav1961.purelib.nanoservice.NanoServiceFactory$AnyKindParserAndSetter final 
forQueryP				.parameter	chav1961.purelib.nanoservice.NanoServiceFactory$AnyKindParserAndSetter final 
forRequestHeaderP		.parameter	chav1961.purelib.nanoservice.NanoServiceFactory$AnyKindParserAndSetter final 
forResponseHeaderP		.parameter	chav1961.purelib.nanoservice.NanoServiceFactory$AnyKindParserAndSetter final
forProcessorP			.parameter	java.lang.Object
						.stack 		pessimistic
						aload		this
						dup
						invokespecial	java.lang.Object.Object()V
						dup
						aload		forProcessorP		
						putfield	forProcessor
		.if (totalParsed > 0) 
						dup
						bipush		&totalParsed
						anewarray	char[]
						putfield	forParsedParameters
			.if (pathParsed > 0) 
						dup
						aload		forPathP
						putfield	forPath
			.endif						
			.if (queryParsed > 0) 
						dup
						aload		forQueryP
						putfield	forQuery
			.endif						
			.if (requestHeadParsed > 0) 
						dup
						aload		forRequestHeaderP
						putfield	forRequestHeader
			.endif						
		.endif
		.if (totalResponsed > 0) 
						dup
						aload		forResponseHeaderP		
						putfield	forResponseHeader
						dup
						bipush		&totalResponsed
						anewarray	java.lang.Object
						putfield	forResponsedParameters
		.endif
						return
&simpleName				.end
						.mend

; --------------------------------------------------------------------

getExecutorMethodHead	.macro		type:str,simpleName:str,pathParsed:int=0,queryParsed:int=0,requestHeadParsed:int=0,totalResponsed:int=0
execute					.method		int public throws java.io.IOException, chav1961.purelib.basic.exceptions.ContentException, chav1961.purelib.basic.exceptions.FlowException, chav1961.purelib.basic.exceptions.EnvironmentException
type					.parameter	chav1961.purelib.nanoservice.interfaces.QueryType final
path					.parameter	char[] final 		
query					.parameter	char[] final
requestHeaders			.parameter	com.sun.net.httpserver.Headers final
responseHeaders			.parameter	com.sun.net.httpserver.Headers final
is						.parameter	java.io.InputStream final
os 						.parameter	java.io.OutputStream final
						.stack		pessimistic
sb						.var		java.lang.StringBuilder				
toFlush					.var		java.io.Flushable
toClose					.var		java.io.Closeable
toCloseIn				.var		java.io.Closeable
toXMLStream				.var		javax.xml.stream.XMLStreamWriter
toJsonObject			.var		java.lang.Object
		.if	(totalResponsed > 0)
toOutputHeaders			.var		java.lang.Object[]
		.endif

		.if (pathParsed+queryParsed+requestHeadParsed > 0)						
parsedIndex				.var		int
		.endif
						aconst_null
						dup
						dup
						dup
						dup
						dup
						astore		sb
						astore		toFlush
						astore		toClose
						astore		toCloseIn
						astore		toXMLStream
						astore		toJsonObject
		.if (pathParsed+queryParsed+requestHeadParsed > 0)						
						bipush		0
						istore		parsedIndex
			.if (pathParsed > 0) 
						aload		this
						getfield	forPath
						checkcast	chav1961.purelib.nanoservice.NanoServiceFactory$PathParser
						aload		path
						aload		this
						getfield	forParsedParameters
						iload		parsedIndex
						invokevirtual	chav1961.purelib.nanoservice.NanoServiceFactory$PathParser.parse([C[[CI)I
						istore		parsedIndex
			.endif													
			.if (queryParsed > 0) 
						aload		this
						getfield	forQuery
						checkcast	chav1961.purelib.nanoservice.NanoServiceFactory$QueryParser
						aload		query
						aload		this
						getfield	forParsedParameters
						iload		parsedIndex
						invokevirtual	chav1961.purelib.nanoservice.NanoServiceFactory$QueryParser.parse([C[[CI)I
						istore		parsedIndex
			.endif
			.if (requestHeadParsed > 0)													
						aload		this
						getfield	forRequestHeader
						checkcast	chav1961.purelib.nanoservice.NanoServiceFactory$RequestHeadParser
						aload		requestHeaders
						aload		this
						getfield	forParsedParameters
						iload		parsedIndex
						invokevirtual	chav1961.purelib.nanoservice.NanoServiceFactory$RequestHeadParser.parse(Lcom/sun/net/httpserver/Headers;[[CI)I
						istore		parsedIndex
			.endif
		.endif
		.if (totalResponsed > 0) 
						aload		this
						getfield	forResponseHeader
						checkcast	chav1961.purelib.nanoservice.NanoServiceFactory$ResponseHeadSetter
						bipush		&totalResponsed
						anewarray	java.lang.Object
						dup
						astore		toOutputHeaders		
						invokevirtual	chav1961.purelib.nanoservice.NanoServiceFactory$ResponseHeadSetter.prepare([Ljava/lang/Object;)I
						pop
		.endif
						aload		this
						getfield	forProcessor		
						checkcast	&type	
						.mend

; --------------------------------------------------------------------

getExecutor2PrimitiveArray	.macro	className:str,parameterIndex:int=
labelId					.local		str
labelId					.set		uniqueG()#"_"#uniqueL()
						aload		this
						getfield	forParsedParameters
						bipush		&parameterIndex
						aaload
						dup
						ifnull		skip&labelId
		.choise className
			.of "boolean"
						invokestatic	chav1961.purelib.nanoservice.InternalUtils.buildBooleanArray([C)[Z
			.of "byte"
						invokestatic	chav1961.purelib.nanoservice.InternalUtils.buildByteArray([C)[B
			.of "short"
						invokestatic	chav1961.purelib.nanoservice.InternalUtils.buildShortArray([C)[S
			.of "int"
						invokestatic	chav1961.purelib.nanoservice.InternalUtils.buildIntArray([C)[I
			.of "long"
						invokestatic	chav1961.purelib.nanoservice.InternalUtils.buildLongArray([C)[J
			.of "float"
						invokestatic	chav1961.purelib.nanoservice.InternalUtils.buildFloatArray([C)[F
			.of "double"
						invokestatic	chav1961.purelib.nanoservice.InternalUtils.buildDoubleArray([C)[D
			.of "char"
			.otherwise
				.error "??????"
		.endchoise
						goto		contunue&labelId
skip&labelId:			aconst_null
contunue&labelId:						
						.mend

; --------------------------------------------------------------------

getExecutor2StringArray	.macro	parameterIndex:int=
			.if parameterIndex >= 0
						aload		this
						getfield	forParsedParameters
						bipush		&parameterIndex
						aaload
						invokestatic	chav1961.purelib.nanoservice.InternalUtils.buildStringArray([C)Ljava/lang/String;
			.else
						bipush		0
						anewarray	java.lang.String.class	
			.endif
						.mend

; --------------------------------------------------------------------

getExecutor2Primitive	.macro	className:str,parameterIndex:int=
						aload		this
						getfield	forParsedParameters
						bipush		&parameterIndex
						aaload
		.choise className
			.of "boolean"
						invokestatic	chav1961.purelib.nanoservice.InternalUtils.buildBoolean([C)Z
			.of "byte"
						invokestatic	chav1961.purelib.nanoservice.InternalUtils.buildByte([C)B
			.of "short"
						invokestatic	chav1961.purelib.nanoservice.InternalUtils.buildShort([C)S
			.of "int"
						invokestatic	chav1961.purelib.nanoservice.InternalUtils.buildInt([C)I
			.of "long"
						invokestatic	chav1961.purelib.nanoservice.InternalUtils.buildLong([C)J
			.of "float"
						invokestatic	chav1961.purelib.nanoservice.InternalUtils.buildFloat([C)F
			.of "double"
						invokestatic	chav1961.purelib.nanoservice.InternalUtils.buildDouble([C)D
			.of "char"
				.error "??????"
			.otherwise
				.error "??????"
		.endchoise
						nop
						.mend

; --------------------------------------------------------------------

getExecutor2String		.macro	parameterIndex:int=
label					.local		str
label					.set		uniqueG()#"_"#uniqueL()
			.if parameterIndex >= 0
						aload		this
						getfield	forParsedParameters
						bipush		&parameterIndex
						aaload
						ifnull		skip&label
						new			java.lang.String
						dup
						aload		this
						getfield	forParsedParameters
						bipush		&parameterIndex
						aaload
						invokespecial	java.lang.String.String([C)V
						goto		continue&label		
skip&label:				aconst_null
continue&label:
			.else
						aconst_null
			.endif
						.mend

; --------------------------------------------------------------------

getExecutor2UUID		.macro	parameterIndex:int=
						getExecutor2String	parameterIndex=&parameterIndex
						invokestatic		java.util.UUID.fromString(Ljava/lang/String;)Ljava/util/UUID;
						.mend


; --------------------------------------------------------------------

getExecutor2StringBuilderOut		.macro		parameterIndex:int=
						aload		toOutputHeaders
						dup
						bipush		&parameterIndex
						new			java.lang.StringBuilder
						dup
						invokespecial	java.lang.StringBuilder.StringBuilder()V	
						aastore
						bipush		&parameterIndex
						aaload
						checkcast	java.lang.StringBuilder
						.mend	

; --------------------------------------------------------------------

getExecutor2ListOut		.macro		parameterIndex:int=
						aload		toOutputHeaders
						dup
						bipush		&parameterIndex
						new			java.util.ArrayList
						dup
						invokespecial	java.util.ArrayList.ArrayList()V	
						aastore
						bipush		&parameterIndex
						aaload
						checkcast	java.util.List
						.mend	

; --------------------------------------------------------------------

getExecutor2ClassOut				.macro	className:str,parameterIndex:int=
						aload		toOutputHeaders
						dup
						bipush		&parameterIndex
						new			&className
						dup
						invokespecial	&className..<init>()V	
						aastore
						bipush		&parameterIndex
						aaload
						checkcast	&className
						.mend	

; --------------------------------------------------------------------

OutputStream2Stack		.macro
						aload		os
						.mend

; --------------------------------------------------------------------

Writer2Stack			.macro
						aload		os
						aload		requestHeaders
						invokestatic	chav1961.purelib.nanoservice.InternalUtils.buildWriter(Ljava/io/OutputStream;Lcom/sun/net/httpserver/Headers;)Ljava/io/Writer;
						dup
						astore		toFlush
						.mend

; --------------------------------------------------------------------

XMLStreamWriter2Stack	.macro
						aload		os
						aload		requestHeaders
						invokestatic	chav1961.purelib.nanoservice.InternalUtils.buildXMLStreamWriter(Ljava/io/OutputStream;Lcom/sun/net/httpserver/Headers;)Ljavax/xml/stream/XMLStreamWriter;
						dup
						astore		toXMLStream
						.mend

; --------------------------------------------------------------------

Document2Stack			.macro
						aload		os
						aload		requestHeaders
						invokestatic	chav1961.purelib.nanoservice.InternalUtils.buildDocument(Ljava/io/OutputStream;Lcom/sun/net/httpserver/Headers;)Lorg/w3c/dom/Document;
						dup
						checkcast	java.io.Closeable
						astore		toClose
						.mend

; --------------------------------------------------------------------

CreoleWriter2Stack		.macro		writerType:str
						new			chav1961.purelib.streams.char2char.CreoleWriter
						dup
						aload		os
						aload		requestHeaders
						invokestatic	chav1961.purelib.nanoservice.InternalUtils.buildWriter(Ljava/io/OutputStream;Lcom/sun/net/httpserver/Headers;)Ljava/io/Writer;
						getstatic		chav1961.purelib.enumerations.MarkupOutputFormat.&writerType
						invokespecial	chav1961.purelib.streams.char2char.CreoleWriter.CreoleWriter(Ljava/io/Writer;Lchav1961/purelib/enumerations/MarkupOutputFormat;)V
						dup						
						astore		toClose
						.mend

; --------------------------------------------------------------------

CharacterTarget2Stack	.macro
						new			chav1961.purelib.streams.chartarget.WriterCharTarget
						dup
						aload		os
						aload		requestHeaders
						invokestatic	chav1961.purelib.nanoservice.InternalUtils.buildWriter(Ljava/io/OutputStream;Lcom/sun/net/httpserver/Headers;)Ljava/io/Writer;
						dup
						astore		toFlush
						ldc			0
						invokespecial	chav1961.purelib.streams.chartarget.WriterCharTarget.WriterCharTarget(Ljava/io/Writer;Z)V
						.mend

; --------------------------------------------------------------------

JsonStaxPrinter2Stack	.macro
						new			chav1961.purelib.streams.JsonStaxPrinter
						dup
						aload		os
						aload		requestHeaders
						invokestatic	chav1961.purelib.nanoservice.InternalUtils.buildWriter(Ljava/io/OutputStream;Lcom/sun/net/httpserver/Headers;)Ljava/io/Writer;
						invokespecial	chav1961.purelib.streams.JsonStaxPrinter.JsonStaxPrinter(Ljava/io/Writer;)V
						dup
						dup
						astore		toFlush
						astore		toClose
						.mend

; --------------------------------------------------------------------

JsonSerializable2Stack	.macro		className:str,constructorName:str
						new			&className
						dup
						invokespecial	&className..&constructorName()V
						dup
						astore		toJsonObject
						.mend

; --------------------------------------------------------------------

StringBuilder2Stack		.macro
						new			java.lang.StringBuilder
						dup
						invokespecial	java.lang.StringBuilder.StringBuilder()V
						dup
						astore		sb
						.mend

; --------------------------------------------------------------------

getExecutorMethodTail	.macro		responsePresent:bool=false
		.if (responsePresent)
						aload		this
						getfield	forResponseHeader
						checkcast	chav1961.purelib.nanoservice.NanoServiceFactory$ResponseHeadSetter
						aload		responseHeaders
						aload		toOutputHeaders
						invokevirtual	chav1961.purelib.nanoservice.NanoServiceFactory$ResponseHeadSetter.commit(Lcom/sun/net/httpserver/Headers;[Ljava/lang/Object;)I
						pop
		.endif
						aload		toFlush
						ifnull		skip1
						aload		toFlush
						invokeinterface	java.io.Flushable.flush()V
skip1:					aload		sb
						ifnull		skip2
						aload		sb
						aload		os						
						aload		requestHeaders
						invokestatic	chav1961.purelib.nanoservice.InternalUtils.dumpToOuputStream(Ljava/lang/StringBuilder;Ljava/io/OutputStream;Lcom/sun/net/httpserver/Headers;)V
skip2:					aload		toClose
						ifnull		skip3
						aload		toClose
						invokeinterface	java.io.Closeable.close()V
skip3:					aload		toXMLStream
						ifnull		skip4
						aload		toXMLStream
						dup
						invokeinterface	javax.xml.stream.XMLStreamWriter.flush()V
						invokeinterface	javax.xml.stream.XMLStreamWriter.close()V
skip4:					aload		toJsonObject
						ifnull		skip5
						aload		toJsonObject
						aload		os						
						aload		requestHeaders
						invokestatic	chav1961.purelib.nanoservice.InternalUtils.dumpToJsonOutputStream(Ljava/lang/Object;Ljava/io/OutputStream;Lcom/sun/net/httpserver/Headers;)V
skip5:			 		ireturn
execute					.end
						.mend

; --------------------------------------------------------------------

callError				.macro		message:str
						aload_0
						iload		from
						ldc			'&message'
						invokevirtual	chav1961.purelib.ui.microservice.MicroServiceFactory$AnyKindParser(Ljava/lang/String;I[Ljava/lang/String;)V
						.mend

; --------------------------------------------------------------------

makePathIncludes 		.macro
						.package 	chav1961.purelib.nanoservice
						.import		chav1961.purelib.basic.CharUtils
						.import		chav1961.purelib.basic.exceptions.ContentException
						.import		chav1961.purelib.basic.exceptions.FlowException
						.import		chav1961.purelib.basic.exceptions.EnvironmentException
						.import		chav1961.purelib.basic.exceptions.SyntaxException
						.import		chav1961.purelib.enumerations.MarkupOutputFormat
						.import		chav1961.purelib.streams.chartarget.WriterCharTarget
						.import		chav1961.purelib.streams.char2char.CreoleWriter
						.import		chav1961.purelib.streams.JsonStaxPrinter
						.import		chav1961.purelib.nanoservice.NanoServiceFactory$PathParser
						.import		chav1961.purelib.nanoservice.NanoServiceFactory$QueryParser
						.import		chav1961.purelib.nanoservice.NanoServiceFactory$RequestHeadParser
						.import		chav1961.purelib.nanoservice.NanoServiceFactory$AnyKindParserAndSetter
						.import		chav1961.purelib.basic.exceptions.SyntaxException
						.import		chav1961.purelib.nanoservice.interfaces.MethodExecutor
						.import		chav1961.purelib.nanoservice.interfaces.MultipartContent
						.import		chav1961.purelib.nanoservice.interfaces.QueryType
						.import		chav1961.purelib.nanoservice.InternalUtils
						.import		java.util.Arrays
						.mend

; --------------------------------------------------------------------
	
getPathParserClassHead	.macro		name:str
&name					.class		public extends chav1961.purelib.nanoservice.NanoServiceFactory$PathParser
template				.field		char[] final
						.mend


; --------------------------------------------------------------------
	
getPathParserConstructorHead	.macro		name:str
&name					.method		void public
						.stack		optimistic
						aload_0
						invokespecial	chav1961.purelib.nanoservice.NanoServiceFactory$PathParser.PathParser()V
						.mend

; --------------------------------------------------------------------

preparePathParserClass 	.macro	index:int,content:str
						aload_0
						ldc_w		"&content"
						invokevirtual	java.lang.String.toCharArray()[C
						putfield	compare&index
						.mend

; --------------------------------------------------------------------
	
getPathParserConstructorTail	.macro		name:str
						return
&name					.end
						.mend

; --------------------------------------------------------------------
	
getPathParserMethodHead	.macro
parse					.method		int public throws chav1961.purelib.basic.exceptions.SyntaxException
path					.parameter	char[] final 
target					.parameter	char[][] final 
from					.parameter	int final 
						.stack 		pessimistic
currentIndex			.var		int
currentElement			.var		int
startIndex				.var		int
						iconst_0						
						istore		currentIndex
						iload		from
						istore		currentElement
						.mend

; --------------------------------------------------------------------

getPathParserCompareSlash			.macro
						aload		path
						arraylength
						iload		currentIndex
						if_icmple	fail
						aload		path
						iload		currentIndex
						caload
						ldc			'/'
						if_icmpne	fail
						iinc		currentIndex,1
						.mend
						
; --------------------------------------------------------------------

getPathParserCompareConstantPath	.macro	index:int,step:int
						aload		path
						iload		currentIndex
						aload_0
						getfield	compare&index
						invokestatic	chav1961.purelib.basic.CharUtils.compare([CI[C)Z
						ifeq		fail
						iinc		currentIndex,&step
						.mend

; --------------------------------------------------------------------

getPathParserExtractValue			.macro
						aload		target
						iload		currentElement
						aload		path
						iload		currentIndex
						invokestatic	chav1961.purelib.nanoservice.NanoServiceFactory$PathParser.extractName([CI)[C
						dup
						arraylength
						iload		currentIndex
						iadd
						istore		currentIndex
						aastore
						iinc		currentElement,1
						.mend

; --------------------------------------------------------------------
	
getPathParserExtractTail			.macro
						aload		target
						iload		currentElement
						aload		path
						dup
						arraylength
						iload		currentIndex
						swap
						invokestatic	java.util.Arrays.copyOfRange([CII)[C
						dup
						arraylength
						iload		currentIndex
						iadd
						istore		currentIndex
						aastore
						iinc		currentElement,1
						.mend	
	
; --------------------------------------------------------------------

getPathParserMethodTail	.macro
						aload		path
						arraylength
						iload		currentIndex
						if_icmpne	fail
						iload		currentElement
						ireturn
fail:					iconst_m1						
						ireturn
parse					.end
						.mend

; --------------------------------------------------------------------

getPathParserClassTail	.macro		name:str
&name					.end
						.mend

; --------------------------------------------------------------------

makeQueryIncludes 		.macro
						.package 	chav1961.purelib.nanoservice
						.import		chav1961.purelib.basic.CharUtils
						.import		chav1961.purelib.basic.exceptions.ContentException
						.import		chav1961.purelib.basic.exceptions.FlowException
						.import		chav1961.purelib.basic.exceptions.EnvironmentException
						.import		chav1961.purelib.basic.exceptions.SyntaxException
						.import		chav1961.purelib.enumerations.MarkupOutputFormat
						.import		chav1961.purelib.streams.chartarget.WriterCharTarget
						.import		chav1961.purelib.streams.char2char.CreoleWriter
						.import		chav1961.purelib.streams.JsonStaxPrinter
						.import		chav1961.purelib.nanoservice.NanoServiceFactory$PathParser
						.import		chav1961.purelib.nanoservice.NanoServiceFactory$QueryParser
						.import		chav1961.purelib.nanoservice.NanoServiceFactory$RequestHeadParser
						.import		chav1961.purelib.nanoservice.NanoServiceFactory$AnyKindParserAndSetter
						.import		chav1961.purelib.basic.exceptions.SyntaxException
						.import		chav1961.purelib.nanoservice.interfaces.MethodExecutor
						.import		chav1961.purelib.nanoservice.interfaces.QueryType
						.import		chav1961.purelib.nanoservice.InternalUtils
						.import		java.util.Arrays
						.mend

; --------------------------------------------------------------------
	
getQueryParserClassHead	.macro		name:str,parameterCount:int=
index		.local		int
&name					.class		public extends chav1961.purelib.nanoservice.NanoServiceFactory$QueryParser
			.for index = 0 to parameterCount-1 step 1
parameter&index			.field		char[] private final
			.endfor
						.mend

; --------------------------------------------------------------------
	
getQueryParserConstructorHead		.macro		name:str,parameterCount:int=
&name					.method		void public
						.stack		optimistic
						aload_0
						invokespecial	chav1961.purelib.nanoservice.NanoServiceFactory$QueryParser.QueryParser()V
						.mend

; --------------------------------------------------------------------

prepareQueryParserClass 	.macro	index:int,content:str
						aload_0
						ldc_w		"&content"
						invokevirtual	java.lang.String.toCharArray()[C
						putfield	parameter&index
						.mend

; --------------------------------------------------------------------

getQueryParserConstructorTail	.macro		name:str
						return
&name					.end
						.mend

; --------------------------------------------------------------------

getQueryParserMethod	.macro	cleans:int=
index		.local		int
parse					.method		int public throws chav1961.purelib.basic.exceptions.SyntaxException
forQuery				.parameter	char[] final 
forTarget				.parameter	char[][] final
forFrom					.parameter	int final
						.stack		pessimistic
currentIndex			.var		int
endIndex				.var		int
endValue				.var		int
currentElement			.var		int
						iconst_0
						dup
						istore		currentIndex
						istore		endValue
						iload		forFrom
						istore		currentElement
						aload		forTarget
			.for index = 1 to cleans step 1
						dup
						iload		currentElement
						aconst_null
						aastore
				.if index != cleans
						iinc		currentElement,1
				.else
						pop
				.endif
			.endfor
						getQueryParserLocateName
						getQueryParserCompareName	parameterCount=&cleans
endOfData:				iload		forFrom
						ldc			&cleans
						iadd
						ireturn
parse					.end
						.mend

; --------------------------------------------------------------------

getQueryParserLocateName	.macro
next:					aload		forQuery
						iload		currentIndex
						invokestatic	chav1961.purelib.nanoservice.NanoServiceFactory$QueryParser.skipParameter([CI)I
						dup
						istore		endIndex
						ifeq		endOfData
						.mend
; --------------------------------------------------------------------

getQueryParserCompareName	.macro	parameterCount:int=
index		.local		int
			.for index = 0 to parameterCount-1 step 1
						aload		forQuery
						iload		currentIndex
						aload		this
						getfield	parameter&index
						invokestatic	chav1961.purelib.basic.CharUtils.compare([CI[C)Z
						ifeq		label&index
						aload		forTarget
						iload		forFrom
				.if index != 0
						ldc			&index
						iadd
				.endif
						aload		forQuery
						iload		endIndex
						invokestatic	chav1961.purelib.nanoservice.NanoServiceFactory$QueryParser.skipValue([CI)I
						istore		endValue
						iinc		endIndex,1
						aload		forQuery
						iload		endIndex
						iload		endValue
						invokestatic	java.util.Arrays.copyOfRange([CII)[C
						aastore
				.if index != parameterCount-1
						goto		skipAmp
				.endif
label&index:			
			.endfor
						aload		forQuery
						iload		endIndex
						invokestatic	chav1961.purelib.nanoservice.NanoServiceFactory$QueryParser.skipValue([CI)I
						goto		testLoop
				.if parameterCount > 1
skipAmp:				iload		endValue
				.endif
testLoop:				dup
						istore		currentIndex
						aload		forQuery
						arraylength
						if_icmpge	endOfData
						aload		forQuery
						iload		currentIndex
						caload
						ldc			'\046'
						if_icmpne	endOfData
						iinc		currentIndex,1
						goto		next
						.mend

; --------------------------------------------------------------------

getQueryParserClassTail	.macro		name:str
&name					.end
						.mend
						
						
; --------------------------------------------------------------------

makeRequestIncludes 	.macro
						.package 	chav1961.purelib.nanoservice
						.import		chav1961.purelib.basic.CharUtils
						.import		chav1961.purelib.basic.exceptions.ContentException
						.import		chav1961.purelib.basic.exceptions.FlowException
						.import		chav1961.purelib.basic.exceptions.EnvironmentException
						.import		chav1961.purelib.basic.exceptions.SyntaxException
						.import		chav1961.purelib.enumerations.MarkupOutputFormat
						.import		chav1961.purelib.streams.chartarget.WriterCharTarget
						.import		chav1961.purelib.streams.char2char.CreoleWriter
						.import		chav1961.purelib.streams.JsonStaxPrinter
						.import		chav1961.purelib.nanoservice.NanoServiceFactory$PathParser
						.import		chav1961.purelib.nanoservice.NanoServiceFactory$QueryParser
						.import		chav1961.purelib.nanoservice.NanoServiceFactory$RequestHeadParser
						.import		chav1961.purelib.nanoservice.NanoServiceFactory$AnyKindParserAndSetter
						.import		chav1961.purelib.basic.exceptions.SyntaxException
						.import		chav1961.purelib.nanoservice.interfaces.MethodExecutor
						.import		chav1961.purelib.nanoservice.interfaces.QueryType
						.import		chav1961.purelib.nanoservice.InternalUtils
						.import		com.sun.net.httpserver.Headers
						.import		java.util.Arrays
						.import		java.util.List
						.mend

; --------------------------------------------------------------------
	
getRequestParserClassHead			.macro		name:str,parameterCount:int=
index		.local		int
&name					.class		public extends chav1961.purelib.nanoservice.NanoServiceFactory$RequestHeadParser
			.for index = 0 to parameterCount-1 step 1
parameter&index			.field		java.lang.String private final
			.endfor
						.mend

; --------------------------------------------------------------------
	
getRequestParserConstructorHead		.macro		name:str,parameterCount:int=
&name					.method		void public
						.stack		optimistic
						aload_0
						invokespecial	chav1961.purelib.nanoservice.NanoServiceFactory$RequestHeadParser.RequestHeadParser()V
						.mend

; --------------------------------------------------------------------

prepareRequestParserClass 			.macro	index:int,content:str
						aload_0
						ldc_w		"&content"
						putfield	parameter&index
						.mend

; --------------------------------------------------------------------

getRequestParserConstructorTail		.macro		name:str
						return
&name					.end
						.mend

; --------------------------------------------------------------------
						
getRequestParserMethod	.macro		parameterCount:int=
index		.local	int
parse					.method		int public throws chav1961.purelib.basic.exceptions.SyntaxException
forHeaders				.parameter	com.sun.net.httpserver.Headers final
forTarget				.parameter	char[][] final
forFrom					.parameter	int final
						.stack		pessimistic
			.for index = 0 to parameterCount-1 step 1
						aload		forTarget		
						ldc			&index
						aconst_null
						aastore
			.endfor
			.for index = 0 to parameterCount-1 step 1
						aload		forTarget		
						ldc			&index
						aload		forHeaders
						aload_0
						getfield	parameter&index
						invokevirtual	com.sun.net.httpserver.Headers.get(Ljava/lang/Object;)Ljava/util/List;
						dup
						ifnull		skip&index
						invokestatic	chav1961.purelib.nanoservice.NanoServiceFactory$RequestHeadParser.list2charArray(Ljava/util/List;)[C
skip&index:				aastore
			.endfor
						ldc			&parameterCount
						ireturn
parse					.end						
						.mend
						
; --------------------------------------------------------------------
						
getRequestParserClassTail			.macro		name:str
&name					.end
						.mend
						
; --------------------------------------------------------------------

makeResponseIncludes 	.macro		additionalClasses:str
className				.local		str
						.package 	chav1961.purelib.nanoservice
						.import		chav1961.purelib.basic.CharUtils
						.import		chav1961.purelib.basic.exceptions.ContentException
						.import		chav1961.purelib.basic.exceptions.FlowException
						.import		chav1961.purelib.basic.exceptions.EnvironmentException
						.import		chav1961.purelib.basic.exceptions.SyntaxException
						.import		chav1961.purelib.enumerations.MarkupOutputFormat
						.import		chav1961.purelib.streams.chartarget.WriterCharTarget
						.import		chav1961.purelib.streams.char2char.CreoleWriter
						.import		chav1961.purelib.streams.JsonStaxPrinter
						.import		chav1961.purelib.nanoservice.NanoServiceFactory$PathParser
						.import		chav1961.purelib.nanoservice.NanoServiceFactory$QueryParser
						.import		chav1961.purelib.nanoservice.NanoServiceFactory$RequestHeadParser
						.import		chav1961.purelib.nanoservice.NanoServiceFactory$ResponseHeadSetter
						.import		chav1961.purelib.nanoservice.NanoServiceFactory$AnyKindParserAndSetter
						.import		chav1961.purelib.basic.exceptions.SyntaxException
						.import		chav1961.purelib.nanoservice.interfaces.MethodExecutor
						.import		chav1961.purelib.nanoservice.interfaces.QueryType
						.import		chav1961.purelib.nanoservice.InternalUtils
						.import		com.sun.net.httpserver.Headers
						.import		java.util.Arrays
						.import		java.util.List
						.import		java.lang.Integer
						.import		java.lang.NullPointerException
						.import		java.lang.IllegalArgumentException
		.forall className in additionalClasses splitted by ","
						.import		&className
		.endforall 
						.mend

; --------------------------------------------------------------------
	
getResponseSetterClassHead			.macro		name:str,parameterCount:int=
index		.local		int
&name					.class		public extends chav1961.purelib.nanoservice.NanoServiceFactory$ResponseHeadSetter
			.for index = 0 to parameterCount-1 step 1
parameter&index			.field		java.lang.String[] private final
			.endfor
						.mend

; --------------------------------------------------------------------
	
getResponseSetterConstructorHead		.macro		name:str,parameterCount:int=
&name					.method		void public
						.stack		optimistic
						aload_0
						invokespecial	chav1961.purelib.nanoservice.NanoServiceFactory$ResponseHeadSetter.ResponseHeadSetter()V
						.mend

; --------------------------------------------------------------------

prepareResponseSetterClass 			.macro	content:str,size:int=1,fieldIndex:int=
index					.local		int
value					.local		str
						aload_0
						ldc			&size
						anewarray	java.lang.String
index					.set		0						
						.forall		value in content splitted by ","
						dup
						ldc			&index
						ldc			"&value"
						aastore
index					.set		index+1						
						.endforall
						putfield	parameter&fieldIndex
						.mend

; --------------------------------------------------------------------

getResponseSetterConstructorTail		.macro		name:str
						return
&name					.end
						.mend


; --------------------------------------------------------------------

getResponseSetterPreparator	.macro	classNames:str,size:int=
index					.local		int
className				.local		str
prepare					.method		int public throws chav1961.purelib.basic.exceptions.SyntaxException
objArray				.parameter	java.lang.Object[] final
						.stack		10
						aload		objArray
						ifnull		fail1
						aload		objArray
						arraylength
						bipush		&size
						if_icmpne	fail2
index					.set		0						
						.forall		className in classNames splitted by ","
						aload		objArray
						bipush		&index
						new			&className
						dup
						invokespecial	&className..<init>()V
						aastore
index					.set		index+1						
						.endforall
						bipush		&size
						ireturn
fail1:					new			java.lang.NullPointerException
						dup
						ldc			"Object array to fill can't be null"
						invokespecial	java.lang.NullPointerException.NullPointerException(Ljava/lang/String;)V
						athrow										
fail2:					new			java.lang.IllegalArgumentException
						dup
						ldc			"Object array to fill has illegal size [%1$d], must be [%2$d]"
						bipush		2
						anewarray	java.lang.Object
						dup
						iconst_0
						aload		objArray
						arraylength
						invokestatic	java.lang.Integer.valueOf(I)Ljava/lang/Integer;
						aastore
						dup
						iconst_1
						bipush		&size
						invokestatic	java.lang.Integer.valueOf(I)Ljava/lang/Integer;
						aastore
						invokestatic	java.lang.String.format(Ljava/lang/String;[Ljava/lang/Object;)Ljava/lang/String;
						invokespecial	java.lang.IllegalArgumentException.IllegalArgumentException(Ljava/lang/String;)V
						athrow
prepare					.end						
						.mend

; --------------------------------------------------------------------

getResponseSetterCommitterHead	.macro	classNames:str,size:int=
commit					.method		int public throws chav1961.purelib.basic.exceptions.SyntaxException
header					.parameter	com.sun.net.httpserver.Headers final
instances				.parameter	java.lang.Object[] final
						.stack		10
						aload		header
						ifnull		fail1
						aload		instances
						ifnull		fail2
						aload		instances
						arraylength
						bipush		&size
						if_icmpne	fail3
						.mend

; --------------------------------------------------------------------

getResponseSetterCommitterSB	.macro	parameterIndex:int=
						aload		header
						aload_0
						getfield	parameter&parameterIndex
						iconst_0
						aaload
						aload		instances
						bipush		&parameterIndex
						aaload
						checkcast	java.lang.StringBuilder
						invokevirtual	java.lang.StringBuilder.toString()Ljava/lang/String;
						invokevirtual	com.sun.net.httpserver.Headers.set(Ljava/lang/String;Ljava/lang/String;)V
						.mend

; --------------------------------------------------------------------

getResponseSetterCommitterList	.macro	parameterIndex:int=
uniqueLabel				.local		str
uniqueLabel				.set		uniqueG()#"_"#uniqueL()
						.begin
array					.var		java.lang.String[] final
arrayIndex				.var		int
						aload		instances
						bipush		&parameterIndex
						aaload
						checkcast	java.util.List
						invokestatic	chav1961.purelib.nanoservice.NanoServiceFactory$ResponseHeadSetter.list2StringArray(Ljava/util/List;)[Ljava/lang/String;
						dup
						astore		array
						arraylength
						ifeq		skip&uniqueLabel				
						iconst_0
						istore		arrayIndex								
label&uniqueLabel:		aload		header
						aload_0
						getfield	parameter&parameterIndex
						iconst_0
						aaload
						aload		array
						iload		arrayIndex
						aaload
						invokevirtual	com.sun.net.httpserver.Headers.add(Ljava/lang/String;Ljava/lang/String;)V
						iinc		arrayIndex,1
						aload		array
						arraylength
						iload		arrayIndex
						if_icmpne	label&uniqueLabel
skip&uniqueLabel:
						.end
						.mend

; --------------------------------------------------------------------

getResponseSetterCommitterClassStart	.macro	parameterIndex:int=
						aload		instances
						bipush		&parameterIndex
						aaload
						.mend

; --------------------------------------------------------------------

getResponseSetterCommitterClassStore	.macro	className:str,fieldName:str,parameterIndex:int=,nameIndex:int=,type:str=
						dup
						aload			header
						swap
						checkcast		&className
						getfield		&className..&fieldName
			.choise		type
				.of			"byte"
						invokestatic	java.lang.String.valueOf(I)Ljava/lang/String;
				.of			"short"
						invokestatic	java.lang.String.valueOf(I)Ljava/lang/String;
				.of			"int"
						invokestatic	java.lang.String.valueOf(I)Ljava/lang/String;
				.of			"long"
						invokestatic	java.lang.String.valueOf(L)Ljava/lang/String;
				.of			"float"
						invokestatic	java.lang.String.valueOf(F)Ljava/lang/String;
				.of			"double"
						invokestatic	java.lang.String.valueOf(D)Ljava/lang/String;
				.of			"char"
						invokestatic	java.lang.String.valueOf(C)Ljava/lang/String;
				.of			"boolean"
						invokestatic	java.lang.String.valueOf(Z)Ljava/lang/String;
				.otherwise
						invokestatic	chav1961.purelib.nanoservice.NanoServiceFactory$ResponseHeadSetter.toString(Ljava/lang/Object;)Ljava/lang/String;
			.endchoise
						aload_0
						getfield		parameter&parameterIndex
						bipush			&nameIndex
						aaload
						swap
						invokevirtual	com.sun.net.httpserver.Headers.set(Ljava/lang/String;Ljava/lang/String;)V
;						pop2
						.mend

; --------------------------------------------------------------------

getResponseSetterCommitterClassEnd	.macro	parameterIndex:int=
						pop
						.mend

; --------------------------------------------------------------------
						
getResponseSetterCommitterTail	.macro	size:int=
						bipush		&size
						ireturn
fail1:					new			java.lang.NullPointerException
						dup
						ldc			"Header can't be null"
						invokespecial	java.lang.NullPointerException.NullPointerException(Ljava/lang/String;)V
						athrow										
fail2:					new			java.lang.NullPointerException
						dup
						ldc			"Instances array can't be null"
						invokespecial	java.lang.NullPointerException.NullPointerException(Ljava/lang/String;)V
						athrow										
fail3:					new			java.lang.IllegalArgumentException
						dup
						ldc			"Object array to fill has illegal size [%1$d], must be [%2$d]"
						bipush		2
						anewarray	java.lang.Object
						dup
						iconst_0
						aload		instances
						arraylength
						invokestatic	java.lang.Integer.valueOf(I)Ljava/lang/Integer;
						aastore
						dup
						iconst_1
						bipush		&size
						invokestatic	java.lang.Integer.valueOf(I)Ljava/lang/Integer;
						aastore
						invokestatic	java.lang.String.format(Ljava/lang/String;[Ljava/lang/Object;)Ljava/lang/String;
						invokespecial	java.lang.IllegalArgumentException.IllegalArgumentException(Ljava/lang/String;)V
						athrow
commit					.end
						.mend


; --------------------------------------------------------------------
						
getResponseSetterClassTail			.macro		name:str
&name					.end
						.mend
						
; --------------------------------------------------------------------
						
PostString2Stack		.macro
						PostReader2Stack
						invokestatic	chav1961.purelib.basic.Utils.fromResource(Ljava/io/Reader;)Ljava/lang/String;
						.mend

; --------------------------------------------------------------------
						
PostCharacterSource2Stack		.macro
						new 	chav1961.purelib.streams.charsource.ReaderCharSource
						dup
						PostReader2Stack
						bipush	1
						invokespecial chav1961.purelib.streams.charsource.ReaderCharSource.ReaderCharSource(Ljava/io/Reader;Z)V
						.mend

; --------------------------------------------------------------------
						
PostReader2Stack		.macro
						PostInputStream2Stack
						aload	requestHeaders
						invokestatic	chav1961.purelib.nanoservice.InternalUtils.buildReader(Ljava/io/InputStream;Lcom/sun/net/httpserver/Headers;)Ljava/io/Reader;
						dup
						astore	toCloseIn
						.mend

; --------------------------------------------------------------------
						
PostInputStream2Stack	.macro
						aload	is
						.mend

; --------------------------------------------------------------------

PostMultipartContent2Stack	.macro
						aload	is
						invokestatic	chav1961.purelib.nanoservice.InternalUtils.buildMultipartContent(Ljava/io/InputStream;)Lchav1961/purelib/nanoservice/interfaces/MultipartContent;
						.mend


; --------------------------------------------------------------------
						
PostDocument2Stack		.macro
						PostInputStream2Stack
						aload	requestHeaders
						invokestatic	chav1961.purelib.nanoservice.InternalUtils.buildDocument(Ljava/io/InputStream;Lcom/sun/net/httpserver/Headers;)Lorg/w3c/dom/Document;
						.mend

; --------------------------------------------------------------------
						
PostXMLStreamReader2Stack		.macro
						PostInputStream2Stack
						aload	requestHeaders
						invokestatic	chav1961.purelib.nanoservice.InternalUtils.buildXMLStreamReader(Ljava/io/InputStream;Lcom/sun/net/httpserver/Headers;)Ljavax/xml/stream/XMLStreamReader;
						dup
						astore	toCloseIn
						.mend

; --------------------------------------------------------------------
						
PostJsonStaxParser2Stack		.macro
						new		chav1961.purelib.streams.JsonStaxParser
						dup
						PostReader2Stack
						invokespecial	chav1961.purelib.streams.JsonStaxParser.JsonStaxParser(Ljava/io/Reader;)V
						dup
						astore	toCloseIn
						.mend

; --------------------------------------------------------------------
						
PostJsonSerializable2Stack		.macro	className:str,shortClassName:str								
						ldc_w		"&className"
						PostInputStream2Stack
						aload		requestHeaders
						invokestatic	chav1961.purelib.nanoservice.InternalUtils.loadFromJsonInputStream(Ljava/lang/String;Ljava/io/InputStream;Lcom/sun/net/httpserver/Headers;)Ljava/lang/Object;
						checkcast	&className			
						.mend
